(ns doodler.middlewares.state-mode
  (:require [doodler.core :as core]
            [doodler.util :as u]))

(defn- wrap-setup
  [opts]
  (let [setup (:setup opts u/no-fn)]
    (assoc opts
           :setup #(set! core/*state* (setup)))))

(defn- wrap-draw
  [opts]
  (let [draw (:draw opts u/no-fn-state)]
    (assoc opts
           :draw #(set! core/*state* (draw core/*state*)))))

(def events
  [:on-close
   :focus-gained :focus-lost
   :mouse-entered :mouse-exited
   :mouse-pressed :mouse-released
   :mouse-moved :mouse-wheel
   :key-pressed :key-released :key-typed])

(defn wrap-handlers
  [opts]
  (reduce (fn [acc event]
            ;; (prn event (event acc) acc)
            (if-let [handler (event acc)]
              (assoc acc
                     event #(set! core/*state* (handler core/*state* %)))
              acc))
          opts
          events))

(defn state-mode
  [opts]
  (-> opts
      wrap-setup
      wrap-draw
      wrap-handlers))
