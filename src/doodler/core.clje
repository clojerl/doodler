(ns doodler.core
  (:require [doodler.util :as u]
            [doodler.wx.gui :as gui]))

(def ^:dynamic *canvas* nil)

(def ^:dynamic *sketch* nil)

(def ^:dynamic *state* nil)

(defn state
  ([] *state*)
  ([key] (get *state* key)))

(defn set-state!
  "Set sketch-specific state. May only be called once (ideally in the
  setup fn).  Subsequent calls have no effect."
  [& state-vals]
  (when-not *state*
    (let [state-map (apply hash-map state-vals)]
      (set! *state* state-map))))

(defn swap-state!
  "Set sketch-specific state. May only be called once (ideally in the
  setup fn).  Subsequent calls have no effect."
  [f & args]
  (set! *state* (apply f *state* args)))

;; Trigonometry

(def PI (math/pi))
(def HALF-PI    (/ PI 2.0))
(def THIRD-PI   (/ PI 3.0))
(def QUARTER-PI (/ PI 4.0))
(def TWO-PI     (* PI 2.0))

(def DEG-TO-RAD (/ PI 180.0))
(def RAD-TO-DEG (/ 180.0 PI))

(defn cos
  [n]
  (math/cos n))

(defn sin
  [n]
  (math/sin n))

(defn tan
  [n]
  (math/tan n))

(defn acos
  [n]
  (math/acos n))

(defn asin
  [n]
  (math/asin n))

(defn atan
  [n]
  (math/atan n))

(defn atan2
  [y x]
  (math/atan2 y x))

(defn radians
  [degrees]
  (* DEG-TO-RAD degrees))

(defn degrees
  [radians]
  (* RAD-TO-DEG radians))

;; Day & hour

(defn hour
  []
  (let* [#erl[hour _ _] (erlang/time)]
    hour))

(defn minute
  []
  (let* [#erl[_ minute _] (erlang/time)]
    minute))

(defn seconds
  []
  (let* [#erl[_ _ seconds] (erlang/time)]
    seconds))

(defn day
  []
  (let* [#erl[_ _ day] (erlang/date)]
    day))

(defn month
  []
  (let* [#erl[_ month _] (erlang/date)]
    month))

(defn year
  []
  (let* [#erl[year _ _] (erlang/date)]
    year))

;; Numbers

(defn abs
  [n]
  (erlang/abs n))

(defn ceil
  [n]
  (erlang/ceil n))

(defn exp
  [n]
  (math/exp n))

(defn floor
  [n]
  (erlang/trunc n))

(defn log
  [n]
  (math/log n))

(defn pow
  [num exponent]
  (math/pow num exponent))

(defn round
  [n]
  (erlang/round n))

(defn sq
  [n]
  (* n n))

(defn sqrt
  [n]
  (math/sqrt n))

(defn map-range
  [val low1 high1 low2 high2]
  (+ low2
     (* (- high2 low2)
        (/ (- val low1) (- high1 low1)))))

;; Color

(defn color
  ([gray]
   (color gray 255))
  ([gray alpha]
   (color gray gray gray alpha))
  ([r g b]
   (color r g b 255))
  ([r g b alpha]
   #erl[r g b alpha]))

(defn* red
  [#erl[red _ _ _]]
  red)

(defn* green
  [#erl[_ green _ _]]
  green)

(defn* blue
  [#erl[_ _ blue _]]
  blue)

(defn* alpha
  [#erl[r g b alpha]]
  alpha)

(defn* hsb-helper
  [#erl[r g b _]]
  (let* [r (/ r 255)
         g (/ g 255)
         b (/ b 255)
         max (max r g b)
         min (min r g b)
         delta (- max min)]
    #erl[r g b min max delta]))

(defn* hue
  [color]
  (let* [#erl[r g b min max delta] (hsb-helper color)]
    (if (zero? delta)
      0
      (let* [hue (case* max
                   r (/ (- g b) delta)
                   g (+ 2.0 (/ (- b r) delta))
                   b (+ 4.0 (/ (- r g) delta)))
             hue (* hue 60)
             hue (if (< hue 0) (+ hue 360) hue)]
        (round hue)))))

(defn* saturation
  [color]
  (let* [#erl[r g b min max delta] (hsb-helper color)]
    (if (zero? delta)
      0
      (/ delta
         (- 1
            (abs (- (+ min max) 1)))))))

(defn* brightness
  [color]
  (let* [#erl[_ _ _ min max _] (hsb-helper color)]
    (/ (+ min max) 2)))

;; Image

(defn background-image
  [img]
  (wxDC/drawBitmap *canvas* img #erl[0 0]))

(defn create-image
  [w h]
  (wxBitmap/new w h))

;; Stroke & fill

(defn- save-current-stroke
  [color]
  (u/swap-var! *sketch* assoc :current-stroke color))

(defn current-stroke
  []
  (:current-stroke *sketch*))

(defn stroke
  ([gray]
   (stroke gray 255))
  ([gray alpha]
   (stroke gray gray gray alpha))
  ([r g b]
   (stroke r g b 255))
  ([r g b alpha]
   (let [pen (:pen *sketch*)
         color #erl[r g b alpha]]
     (wxPen/setColour pen color)
     (wxDC/setPen *canvas* pen)
     (save-current-stroke color))))

(defn stroke-weight
  [weight]
  (let [pen (:pen *sketch*)]
    (wxPen/setWidth pen weight)
    (wxDC/setPen *canvas* pen)))

(defn stroke-cap
  [cap-mode]
  (let [pen (:pen *sketch*)]
    (wxPen/setCap pen (gui/->stroke-cap cap-mode))
    (wxDC/setPen *canvas* pen)))

(defn stroke-join
  [join-mode]
  (let [pen (:pen *sketch*)]
    (wxPen/setJoin pen (gui/->stroke-join join-mode))
    (wxDC/setPen *canvas* pen)))

(defn background
  ([gray]
   (background gray 255))
  ([gray alpha]
   (background gray gray gray alpha))
  ([r g b]
   (background r g b 255))
  ([r g b alpha]
   (let [brush (:background-brush *sketch*)
         color #erl[r g b alpha]]
     (wxBrush/setColour brush color)
     (wxDC/setBackground *canvas* brush)
     (wxDC/clear *canvas*))))

(defn- save-current-fill
  [color]
  (u/swap-var! *sketch* assoc :current-fill color))

(defn current-fill
  []
  (:current-fill *sketch*))

(defn fill
  ([gray]
   (fill gray 255))
  ([gray alpha]
   (fill gray gray gray alpha))
  ([r g b]
   (fill r g b 255))
  ([r g b alpha]
   (let [brush (:brush *sketch*)
         color #erl[r g b alpha]]
     (wxBrush/setColour brush color)
     (wxDC/setBrush *canvas* brush)
     (save-current-fill color))))

;; Drawing

(defn arc
  [x y w h start end]
  (wxDC/drawEllipticArc *canvas*
                        #erl[(- x (erlang/div w 2)) (- y (erlang/div h 2))]
                        #erl[w h]
                        start end))

(defn ellipse
  [x y w h]
  (wxDC/drawEllipse *canvas*
                    #erl[(- x (erlang/div w 2)) (- y (erlang/div h 2))]
                    #erl[w h]))

(defn line
  ([p1 p2] (apply line (concat p1 p2)))
  ([x1 y1 x2 y2] (wxDC/drawLine *canvas* #erl[x1 y1] #erl[x2 y2])))

(defn point
  [x y]
  (wxDC/drawPoint *canvas* #erl[x y]))

(defn rect
  ([x y w h]
   (wxDC/drawRectangle *canvas* #erl[x y] #erl[w h]))
  ([x y w h r]
   (wxDC/drawRoundedRectangle *canvas* #erl[x y] #erl[w h] r)))

;; Text

(defn text
  [s x y]
  (wxDC/drawText *canvas* s #erl[x y]))

;; Dimension

(defn width
  []
  (:width *sketch*))

(defn height
  []
  (:height *sketch*))

(defn frame-rate
  [new-rate]
  (let [frame-period (u/frame-period new-rate)]
    (set! *sketch*
          (assoc *sketch*
                 :frame-rate new-rate
                 :frame-period frame-period
                 :frame-delay frame-period
                 :current-frame-rate new-rate))))

(defn frame-count
  []
  (*sketch* :frame-count))

(defn target-frame-rate
  []
  (*sketch* :frame-rate))

(defn current-frame-rate
  []
  (*sketch* :current-frame-rate))

(defn millis
  []
  (- (u/time-mark) (*sketch* :epoch)))

(defn no-loop
  []
  (u/swap-var! *sketch* assoc :looping? false))

(defn looping?
  []
  (*sketch* :looping?))

(defn start-loop
  []
  (u/swap-var! *sketch* assoc :looping? true)
  (u/send-message (:pid *sketch*) :refresh))

;; Utils

(defn random
  ([max]
   (dec (rand/uniform max)))
  ([min max]
   (+ min (dec (rand/uniform max)))))

(defn exit
  []
  (erlang/halt 0))

(defmacro defsketch
  [name & opts]
  (let [opts (apply hash-map opts)]
    `(defn ~name []
       (wx_object/start_link :doodler.sketch ~opts #erl()))))
