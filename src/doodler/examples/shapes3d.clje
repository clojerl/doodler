(ns doodler.examples.shapes3d
  (:require [doodler.core :as d]))

(defn setup
  []
  (d/frame-rate 60))

(defn draw-axis []
  (d/with-translation [-100 -100 0]
    ;; X (red)
    (d/stroke 255 0 0)
    (d/line 0 0 0 100 0 0)
    ;; Y (green)
    (d/stroke 0 255 0)
    (d/line 0 0 0 0 100 0)
    ;; Z (blue)
    (d/stroke 0 0 255)
    (d/line 0 0 0 0 0 100)))

(defn draw []
  (d/background 255)

  (draw-axis)

  ;; Choose one or the other to
  ;; see what is the effect
  ;; (d/ortho)
  ;; (d/perspective)

  (d/stroke 0 0 0)
  (d/fill 100 100 255 150)
  (d/ellipse -50 -50 20 20)
  (d/arc -50 50 20 20 0 d/HALF-PI)

  (d/with-translation [0 0 0]
    (d/with-rotation [(/ (d/frame-count) 50) 1.0 1.0 1.0]
      (d/fill 255 100 100 200)
      (d/sphere-detail 30)
      (d/sphere (-> (d/frame-count)
                    (/ 10)
                    d/sin
                    (* 5)
                    (+ 40.0)))
      (d/fill 100 100 255 150)
      (d/box 75.0)))

  (d/with-translation [100 100 0]
    (d/with-rotation [(/ (d/frame-count) 100) 1.0 1.0 0.0]
      (d/cone 20 50)))

  (d/with-translation [100 0 100]
    (d/with-rotation [(/ (d/frame-count) 100) 1.0 1.0 0.0]
      (d/fill 150 255 150)
      (d/sphere-detail 15)
      (d/sphere 15.0))))

(d/defsketch sketch
  :title "3D Shapes"
  :size [500 500]
  :setup setup
  :draw draw
  :features [:resizable :keep-on-top]
  :bgcolor [200]
  :renderer :opengl)
