;; Source: http://quil.info/sketches/show/example_game-of-life
(ns doodler.examples.game-of-life
  (:require [doodler.core :as d]
            [doodler.middlewares.state-mode :as state-mode]))

;;;
;;; Game of Life
;;; https://en.wikipedia.org/wiki/Conway%27s_Game_of_Life
;;;

(def grid-size 50)

(def state {:matrix (vec
                     (repeatedly (* grid-size grid-size) #(rand-int 2)))})

(defn setup []
  (d/frame-rate 10)
  (d/no-stroke)
  state)

(defn get-neighbors [idx vec]
  [
   (get vec (dec (- idx grid-size)))
   (get vec (- idx grid-size))
   (get vec (inc (- idx grid-size)))

   (get vec (dec idx))
   (get vec (inc idx))

   (get vec (dec (+ grid-size idx)))
   (get vec (+ grid-size idx))
   (get vec (inc (+ grid-size idx)))
  ])

(defn new-status [idx itm vec]
  (let [alive-n (get (frequencies (get-neighbors idx vec)) 1 0)]
    (if (= 1 itm)
      (if (or (> alive-n 3) (< alive-n 2)) 0 1)
      (if (= 3 alive-n) 1 0)
    )))

(defn update-state [state]
  (assoc state :matrix
    (vec
      (map-indexed
       (fn [idx itm] (new-status idx itm (:matrix state)))
       (:matrix state)))))

(defn draw-state [state]
  (d/background 240)
  (let [cell-size (quot (d/width) grid-size)]
    (doseq [[i v] (map-indexed vector (:matrix state))]
      (let [multiplier (int (/ i grid-size))
            x (int (* cell-size (- i (* multiplier grid-size))))
            y (int (* cell-size multiplier))]
        (d/fill
         (if (= 1 v) 0 255))
        (d/rect x y cell-size cell-size)))))

(d/defsketch sketch
  :size [500 500]
  :setup setup
  :update update-state
  :draw draw-state
  :middleware [state-mode/state-mode])
